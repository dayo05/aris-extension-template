plugins {
    id 'com.github.johnrengelman.shadow'
    id 'org.jetbrains.kotlin.jvm'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"


    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    modImplementation "me.ddayo:aris-fabric"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mod_package', project.group
    inputs.property 'mod_id', project.modId
    inputs.property 'mod_name', project.modName
    inputs.property 'mod_description', project.modDescription
    inputs.property 'mod_authors', project.modAuthors

    filesMatching('fabric.mod.json') {
        expand(
                version: project.version,
                mod_id: project.modId,
                mod_name: project.modName,
                mod_description: project.modDescription,
                mod_authors: project.modAuthors,
                mod_package: project.group,
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
repositories {
    mavenCentral()
}
kotlin {
    jvmToolchain(17)
}
