plugins {
    id 'com.github.johnrengelman.shadow'
    id 'org.jetbrains.kotlin.jvm'
}

loom {
    forge {
        mixinConfig "${project.modId}.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

repositories {
    // Add KFF Maven repository
    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
    }
    mavenCentral()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
    modImplementation("me.ddayo:aris-forge") { transitive false }
    implementation 'thedarkcolour:kotlinforforge:4.11.0'
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mod_id', project.modId
    inputs.property 'mod_name', project.modName
    inputs.property 'mod_description', project.modDescription
    inputs.property 'mod_authors', project.modAuthors

    filesMatching('META-INF/mods.toml') {
        expand(
                version: project.version,
                mod_id: project.modId,
                mod_name: project.modName,
                mod_description: project.modDescription,
                mod_authors: project.modAuthors,
        )
    }
    filesMatching('pack.mcmeta') {
        expand(
                version: project.version,
                mod_id: project.modId,
                mod_name: project.modName,
                mod_description: project.modDescription,
                mod_authors: project.modAuthors,
        )
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
kotlin {
    jvmToolchain(17)
}
